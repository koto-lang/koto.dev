build: docs playground-release
  zola build

build-debug: docs playground
  zola build

serve *args: docs playground
  zola serve --open --fast {{args}}

serve-release: docs playground-release
  zola serve --open --fast

clean:
  cargo clean
  rm -rf public

deploy:
  git fetch origin
  git checkout deploy
  git reset --hard origin/deploy
  git rebase @^ --onto origin/main
  @just submodules
  @just build
  echo "" >> deployed.txt
  date -u +"%Y-%m-%dT%H:%M:%S%Z" >> deployed.txt
  git rev-parse origin/main >> deployed.txt
  git add deployed.txt
  git add -f public
  git commit --amend --no-edit
  git push -f origin deploy

docs:
  cargo xtask docs

docs-watch:
  cargo watch -w ../modules/koto -w xtask -x "xtask docs"

playground:
  cd playground && trunk build

playground-release:
  cd playground && trunk build --release

playground-serve:
  cd playground && trunk serve --ignore ../static --open

playground-serve-release:
  cd playground && trunk serve --ignore ../static --release --open

submodules:
  git submodule update --init --recursive

test:
  cargo test

update-koto:
  test -z "$(git status --porcelain)" || (echo "Dirty working copy" && exit 1)
  cd ../modules/koto && git fetch && git checkout origin/main
  git add ../modules/koto
  git commit -m "Update to the latest version of Koto"

version-snapshot version: docs playground-release
  cargo xtask version-snapshot {{version}}
  git add content/docs/{{version}}
  git add static/play-{{version}}
  git add templates/data.toml
  git commit -m "Snapshot of version {{version}}"

watch command:
  cargo watch -s "just {{command}}"

worker:
  wrangler dev playground/gist-worker/index.js

worker-setup:
  pnpm install --dir playground/gist-worker

worker-publish:
  wrangler publish playground/gist-worker/index.js
