build $BUILD_MODE="production": npm docs playground-release
  npx vite build
  zola build

build-debug: npm docs playground
  npx vite build
  zola build

serve *args: docs playground
  zola serve --open --fast {{args}} & npx vite serve & wait

serve-release $BUILD_MODE="production": docs playground-release
  zola serve --open --fast & npx vite serve & wait

clean:
  cargo clean
  rm -rf node_modules
  rm -rf public

deploy:
  git fetch origin
  git checkout deploy
  git reset --hard origin/deploy
  git rebase @^ --onto origin/main
  @just submodules
  @just build
  echo "" >> deployed.txt
  date -u +"%Y-%m-%dT%H:%M:%S%Z" >> deployed.txt
  git rev-parse origin/main >> deployed.txt
  git add deployed.txt
  git add -f public
  git commit --amend --no-edit
  git push -f origin deploy

docs: search-index
  cargo xtask docs

docs-watch:
  cargo watch -w ../modules/koto -w xtask -x "xtask docs && xtask search-index"

npm:
  npm install

playground:
  cd playground && trunk build

playground-release:
  cd playground && trunk build --release

playground-serve:
  cd playground && trunk serve --ignore ../static --open

playground-serve-release:
  cd playground && trunk serve --ignore ../static --release --open

search-index:
  cargo xtask search-index

search-index-watch:
  cargo watch -w ../modules/koto -w xtask -x "xtask search-index"

submodules:
  git submodule update --init --recursive

test:
  cargo test

update-koto:
  test -z "$(git status --porcelain)" || (echo "Dirty working copy" && exit 1)
  cd ../modules/koto && git fetch && git checkout origin/main
  git add ../modules/koto
  git commit -m "Update to the latest version of Koto"

version-snapshot version: docs playground-release
  cargo xtask version-snapshot {{version}}
  git add content/docs/{{version}}
  git add static/play-{{version}}
  git add templates/data.toml
  git commit -m "Snapshot of version {{version}}"

vite *args:
  npx vite build {{args}}

watch command:
  cargo watch -s "just {{command}}"

worker:
  wrangler dev playground/gist-worker/index.js

worker-setup:
  pnpm install --dir playground/gist-worker

worker-publish:
  wrangler publish playground/gist-worker/index.js
