# An example of how to build a Set in Koto

# The meta map shared by all Set objects
set_ops =
  @type: 'Set'

  # Allow Sets to be used directly as iterables
  @iterator: || self.keys()

  @meta contains: |value|
    self.data.contains_key value

  @meta keys: ||
    self.data.keys()

  @meta extend: |values|
    for value in values
      self.data.insert value
    self

  @meta insert: |value|
    self.data.insert value
    self

  @meta is_empty: || self.size() == 0
  @meta size: || self.data.size()

# A Set initializer, inserting the given values into the Set
make_set = |values: Iterable? = null|
  # Calling .to_map() on an iterator of single values produces a map with each unique value
  # represented by a key.
  data = values?.to_map() or {}
  # Return a map containing the provided data, along with the Set metamap
  {data}.with_meta set_ops

# An iterator consumer that makes a Set from the iterator's output values
iterator.to_set = || make_set self

# Show a Set in use
@main = ||
  s = make_set()
  s.insert 42
  s.extend (99, 42, 99, -1)
  print s.to_tuple()

@test chars_to_set = ||
  s = 'abccbadeffed'.to_set()
  assert_eq s.keys().to_string(), 'abcdef'
  assert s.contains 'c'
  assert not s.contains 'x'
